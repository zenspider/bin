#!/bin/csh
# a bulk local machine searching engine
# helps find that executable you've been searching for
# 
# Version 1.2
#
# Changes:
# DATE     VER DESCRIPTION
# 11/30/95 1.0 Birth
# 12/08/95 1.1 Added shortcut at beginning to copy my version if exists
# 12/12/95 1.2 Changed expression in grep to normally not take expressions
#              and instead search for EXACT name at end of pathname.

if ($#argv == 0) then
	echo "USAGE: $0:t string1 string2 ..."
	echo "	Performs a bulk search for [string] of all known executables"
	exit(1)
endif

set files=()

foreach string ($*)
    if ($string == "-u") then
        if (-e ~/.apps) then
            echo "Updating application list."
            rm ~/.apps
        else
            echo "Update what? Don't worry about it, I'll handle it."
        endif
    else
        set files=($files $string)
    endif
end

if (! -e ~/.apps) then
    echo "Creating apps list."
    echo "    this will take some time, about a minute, depending on load"
    echo "    this only needs to occur the first time,"
    echo "    or when the -u (update app list) option is used..."
    (find / -perm -1 -type f -print > ~/.apps) >& /dev/null
    echo "...done"
    echo
endif

while ($#files != 0)
    grep '/'$files[1]'$' ~/.apps
    shift files
end
