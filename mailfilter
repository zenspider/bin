#!/usr/bin/perl -w

# Intended to be hooked into qmail via a .qmail file like so:
# 
# |preline /usr/local/bin/spamassassin -PaL | /home/ryand/Bin/mailfilter -f /home/ryand/.mailfilter.rc -m /home/ryand/Mail
# 
# the calling to spamassassin w/ -P is required, -a for autowhitelist,
# -L is optional and makes it slightly faster.

use strict;
use Mail::Audit;
use Time::JulianDay;
use Getopt::Long;

my $VERSION = '1.4.1';

my %option;
GetOptions(\%option,
	   "file=s",
	   "check",
	   "mailfolder=s",
	  );

$option{'check'} ||= '';

# My Mail Policies:
#
# 0) Until trust for this gets built up, we will be saving EVERYTHING 
#    to a single file and logging what we WOULD have done.
# 1) Scan for spam, if it is, move to BlackBox.
# 2) Go through my general filter list, deal w/ matches as directed.
#    this will deal w/ know email addresses, email lists, etc.
#
#    All that should be left is:
#      + suspect spam
#      + addresses I need to add to my list
#
# 3) All other mail goes to GrayBox

$option{mailfolder} = (getpwuid($>))[7] . "/Mail"
  unless $option{mailfolder};

(&log("'$option{mailfolder}' is not a valid directory"), exit(1))
  unless -d $option{mailfolder};

my $logdir = (getpwuid($>))[7];

open LOG, ">>$logdir/.mailfilter.log"
  || die "Couldn't open mailfilter.log: $!";

# Get all of our standard fields of interest.
my ($mail, $from, $to, $cc, $rcpt, $subject) = ('', '', '', '', '', '');
unless ($option{check}) {
  my $debug = 0;
  $mail = Mail::Audit->new();

  my $spam = $mail->get('X-Spam-Status') || '';
  my $is_spam = ($spam =~ m/^yes/i);

  $from = $mail->from() || '';
  $to = $mail->to() || '';
  $cc = $mail->cc() || '';
  $rcpt = join(', ', $mail->to(), $mail->cc(), $mail->bcc());
  $subject = $mail->subject() || '';
  chomp($from, $to, $cc, $subject, $rcpt);

  if ($is_spam) {
    &save("BlackBox", "Spam box:", $to, $subject);
  }
}

# general filter
my $isodate_re = '\d{4}-\d{2}-\d{2}';
my $field_re = 'SUBJECT|FROM|TO|CC|RCPT';

if ($option{file}) {
  unless (open IN, $option{file}) {
    &log("Error opening recipe file '$option{file}': $!");
    exit(1);
  } 
} else {
  *IN = *DATA;
}

while (<IN>) {

  # the usual whitespace cleanup
  chomp;
  s/^\s+//;
  s/\s+$//;

  # ignore comment lines
  next if m/^\#/;
  next if m/^$/;

  my ($cmd, $action, $file, $isodate, $field, $regex, $msg) 
    = ('save', '', '', '', '', '', '');

  if (m/^ACCEPT\s+(\S+)(?:\s+($isodate_re))?\s+($field_re)\s+(.+)/o) {
    $file = $1;
    $isodate = $2 || '';
    $field = lc($3);
    $regex = $4;
    $msg = '';
  } elsif (m/^IGNORE\s+(?:\s+($isodate_re)\s+)?($field_re)\s+(.+)/o) {
    $file = 'BlackBox';
    $isodate = $1 || '';
    $field = lc($2);
    $regex = $3;
    $msg = "Ignoring";
  } elsif (m/^EXEC\s+(\S+)\s+($field_re)\s+(.+)/o) {
    $cmd = 'run';
    $file = $1;
    $field = lc($2);
    $regex = $3;
  } else {
    &log("ERROR: syntax error: '$_'");
    exit(1);
  }

  if ($isodate) {
    $isodate =~ /(\d\d\d\d)-(\d\d)-(\d\d)/;
    my $then = julian_day($1, $2, $3);
    my $now  = local_julian_day(time);

    if ($then < $now) {
      &log("Skipping $field/$regex/$isodate ($then < $now)");
      next;
    }
  }

  unless ($option{check}) {
    my $cmd = "&$cmd(\$file, \$msg, \$field, \$$field, \$regex) if \$$field =~ $regex";
    eval $cmd;
    if ($@) {
      &log("ERROR w/ '$cmd': $@");
    }
  } else {
    &log($file, $msg, $field, $regex);
    my $cmd = "'blah' =~ $regex";
    eval $cmd;
    if ($@) {
      &log("ERROR w/ '$cmd': $@");
      exit(1);
    }
  }
}

unless ($option{check}) {
  &save("GrayBox", "Default rule");
} else {
  &log("Looks good");
}

sub run {
  my $file = shift;

  &log("Running $file");

  $SIG{CHLD} = 'IGNORE';
  unless (fork()) {
    system $file || die "$?";
    exit(0);
  }
}

sub save {
  my $box = shift;

  &log("Saving in '$box'", @_);
  $mail->accept("$option{mailfolder}/$box");
}

sub log {
  my $msg = join(': ', @_) || '';

  $msg =~ s/\s+/ /mg;

  use Date::Format;
  my $time = time2str("%Y%m%d_%H%M%S", time);

  unless ($option{check}) {
    print LOG "$time:$VERSION: $from: $subject: $msg\n";
  } else {
    print "$msg\n";
  }
}

__DATA__
# FORMAT:
# ACCEPT FILE [ISODATE] FIELD REGEX
# IGNORE      [ISODATE] FIELD REGEX
#
# FILE    = some/path (already in mail folder)
# ISODATE = YYYY-MM-DD
# FIELD   = (SUBJECT|FROM|TO|CC|RCPT)
# REGEX   = any perl regex
#
# EXAMPLES:
#
# Ignores any mail whose subject has "Alec Guiness" in it until 2000-09-01
# IGNORE 2000-09-01 SUBJECT m/Alec Guiness/
#
# Accepts mail from "fred@domain.com" into "Personal/Fred"
# ACCEPT Personal/Fred FROM m/fred@domain.com/
#

#CMD   FILE          ISODATE    FIELD   REGEX
