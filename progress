#!/usr/bin/perl -w

use strict;
use File::Find;
use Getopt::Long;

$| = 1;

# TODO: figure out how to pipe through more ONLY if we are interactive
sub I_am_interactive {
    return -t STDIN && -t STDOUT;
}

my @check = ();
#my $keywords = '(?<!SUF|PRE)F IX|H ACK|T ODO|(?<!P|O)D OC(?!UMENT|TYPE|_INSTALL)|RE FACTOR';
my $keywords = '\b(F IX|H ACK|T ODO|D OC|R EFACTOR|R ETIRE|W ARN)\b';
my %hit = ();
my %word = ();
my $total = 0;
my $html = 0;

my %option;
&GetOptions(
	    \%option,
	    'html',
	    'dev',
	   );

@ARGV = ('.') unless @ARGV;

&find(\&wanted, @ARGV);
&check;
&report;
exit;

sub wanted {
  my ($dev,$ino,$mode,$nlink,$uid,$gid) = lstat($_);

  if (-f _ && $_ !~ m/\.(pyc|o|log|diff)$|~$/) {
    return if -B _;
    if ($option{'dev'}) {
      return unless $File::Find::name =~ m%/dev/%;
    }
    push @check, $File::Find::name;
  } else {
    if ($File::Find::name # LEAVE THIS ON TWO LINES - rcs expansion nuking code
	=~ m/CVS|\.svn|blib|html|vendor\/rails$/ || -l _) {
      $File::Find::prune = 1;
    }
  }
}

sub check {

  print STDERR "Found ", scalar @check, " files. Scanning...\n";

  my $count = 0;
  foreach my $file (@check) {
    open IN, $file || die "Couldn't open file '$file': $!";
    my $line = 0;
    while (<IN>) {
      $line++;

      if (m/($keywords)/xo) {
	my $key = $1;
	s/^\s+//;
	s/\s+$//;
	push @{$hit{$file}}, sprintf("%4d: %s", $line, $_);
	$word{$key}++;
	$total++;
      }
    }
    close IN;
    print STDERR 'x'
      unless ++$count % 10;
  }
  print STDERR "done\n\n";

  return %hit;
}

sub report {

  my $summary = &summary;

  local $SIG{PIPE} = sub { die "spooler pipe broke" };

  if (I_am_interactive) {
    my $pager = $ENV{'PAGER'} || 'more';
    open OUT, "| $pager" || die "Couldn't open pipe: $!";
  } else {
    *OUT = *STDOUT;
  }

  print OUT "<PRE>"
    if $option{'html'};

  print OUT $summary;

  print OUT "Detail:\n\n";
  foreach my $file (sort {scalar @{$hit{$b}} <=> scalar @{$hit{$a}} || $a cmp $b} keys %hit) {
    print OUT "${file} (", scalar(@{$hit{$file}}), "):\n\n";
    foreach my $line (@{$hit{$file}}) {

      if ($option{'html'}) {
	$line =~ s|&|&amp;|g;
	$line =~ s|<|&lt;|g;
	$line =~ s|>|&gt;|g;
	$line =~ s|($keywords)|<B>$1</B>|og;
      }

      print OUT $line, "\n";
    }
    print OUT "\n\n";
  }

  print OUT $summary;

  print OUT "</PRE>\n"
    if $option{'html'};

  close OUT if I_am_interactive;
}

sub summary {

  my $summary = '';
  my $checkcount = scalar @check;
  my $hitcount = scalar keys %hit;

  $summary .= "Summary:\n\n";
  $summary .= sprintf "  %-24s : %4d\n", "Number of tags found", $total;
  $summary .= sprintf "  %-24s : %4d (%4.2f%%)\n", "Number of files marked", $hitcount, ($hitcount / $checkcount * 100);
  $summary .= sprintf "  %-24s : %4d\n", "Number of files checked", $checkcount;

  if ($hitcount > 0) {
    $summary .= sprintf "  %-24s : %7.2f\n", "Tags per infected file", ($total / $hitcount);
    $summary .= sprintf "  %-24s : %7.2f\n", "Tags per all files", ($total / $checkcount);
  }

  $summary .= "\n";
  $summary .= "Occurances: \n";
  foreach my $word (sort {$word{$b} <=> $word{$a}} keys %word) {
    $summary .= sprintf "%4d: %s\n", $word{$word}, $word;
  }
  $summary .= "\n";

  return $summary;
}
