#!/bin/csh

set com = $0			# save command name

if ($#argv < 1) then		# check num of arguments
	cat << END
Usage: $com:t <command>
commands:
  findnew - list files to be backed up by incremental backup
  update  - perform an incremental backup
  full    - perform a full backup
  list    - list contents of tape
END
	exit 1
endif

# Do all file processing here:
foreach command ($argv)		# process multiple files

	switch ($command)

	case "findnew":
		find /worsel1/users/ryand /worsel2/users/ryand \
			-newer ~/.last_backup -print
	breaksw

	case "update":
		find /worsel1/users/ryand /worsel2/users/ryand \
			-newer ~/.last_backup -print \
			| cpio -ovC64 > /dev/rmt0.1
		touch ~/.last_backup
	breaksw

	case "full":
		mt rewind
		find /worsel1/users/ryand /worsel2/users/ryand -print \
			| cpio -ovC64 > /dev/rmt0.1
		touch ~/.last_backup
	breaksw

	case "list":
		mt rewind
		cpio -itvC64 < /dev/rmt0.1
	breaksw

	default:
		echo "Unknown command: $command"
	breaksw

	endsw
end
